! ================================================== !
!! # The Evenki morphophonological/twolc rules file 
! ================================================== !

!! This file documents the [phonology.twolc file](http://github.com/giellalt/lang-evn/blob/main/src/fst/phonology.twolc) 

!! ## Alphabet
Alphabet

 а б в г д е ё ж з и й к л м н ӈ о п р с т у ф х ц ч ш щ ъ ы ь э ю я  !!= * @CODE@
 А Б В Г Д Е Ё Ж З И Й К Л М Н Ӈ О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я  !!= * @CODE@

 %{ː%}:̄  !!= * @CODE@

 %{L%}:л %{L%}:р							     !!= * @CODE@
 %{G%}:г %{G%}:к %{G%}:ӈ						     !!= * @CODE@
 %{D%}:д %{D%}:т 							     !!= * @CODE@
 %{A%}:а %{A%}:э %{A%}:о  						     !!= * @CODE@
 %{Я%}:я %{Я%}:е %{Я%}:ё %{Я%}:а %{Я%}:э %{Я%}:о     			     !!= * @CODE@
 %{V%}:в %{V%}:м							     !!= * @CODE@
 %{R%}:р %{R%}:0 %{R%}:т %{R%}:л %{R%}:н 				     !!= * @CODE@
 
 %{j%}:0  %{j%}:ъ		!!= * @CODE@
 %{i%}:0 %{i%}:и %{i%}:ы        !!= * @CODE@
 %{U%}:и %{U%}:у  %{U%}:0  	!!= * @CODE@
 %>  ;       	  		!!= * @CODE@

!! Sets
Sets

Vow = а е ё и о у ы э ю я 						       !!= * @CODE@
      А Е Ё И О У Ы Э Ю Я 						       !!= * @CODE@
      а̄ е̄ ӣ э̄ ӯ о̄							       !!= * @CODE@
      А̄ Е̄ Ӣ ¯Э Ӯ О̄ ;						       !!= * @CODE@

Cns = б в г д ж з й к л м н ӈ п р с т ф х ц ч ш щ      !!= * @CODE@
      Б В Г Д Ж З Й К Л М Н Ӈ П Р С Т Ф Х Ц Ч Ш Щ ;    !!= * @CODE@
ArchiCns = %{D%} %{G%} %{L%} %{R%} %{V%} ;			       !!= * @CODE@
Nasal = м н ӈ									       !!= * @CODE@
        М Н Ӈ ; 								       !!= * @CODE@

NonLN = б в г д ж з й к м ӈ п р с т ф х ц ч ш щ        !!= * @CODE@
        Б В Г Д Ж З Й К М Ӈ П Р С Т Ф Х Ц Ч Ш Щ ;      !!= * @CODE@

UnvoicedCns = к с т п ;    !!= * @CODE@
VoicedCns1 = б в г д щ з й р ; !!= * @CODE@
!VoicedCns1 = б в г д щ з й м р ; 


!! ## Rules
Rules 

!! ### Assimilation rules

"Remove suffix vowel with vowel-ending stem"
!! **@RULENAME@**  
%{i%}:0 <=>  :Vow (%{ː%}:) %> _ ;

"Realise suffix vowel as y after t"
!! **@RULENAME@**  
%{i%}:ы <=> :т %> _ ;

"Realise suffix vowel as i after non-t"
!! **@RULENAME@**  
%{i%}:и <=> [:Cns - :т] %> _ ;

"Assimilate possessive to plural"
!! **@RULENAME@**  
%{L%}:Cx <=> Cx %> _ ;
       where Cx in ( л р ) ;

"Consonant assimilation in suffixes"
!! **@RULENAME@**  
%{G%}:Cx <=> Cy %> _ ;
       where Cy in ( к с т п м н ӈ ) 
             Cx in ( к к к к ӈ ӈ ӈ ) 
       matched ; 

"Unvoicing assimilation"
!! **@RULENAME@**  
%{D%}:т <=> :UnvoicedCns %> _ ; 

!!€ дэт>{D}ук
!!€ дэт>тук

"Voicing assimilation"
!! **@RULENAME@**  
!%{D%}:д <=> [:Vow| %{ː%}: | [:Cns - :UnvoicedCns]] %> _ ; 
%{D%}:д <=> [Vow|л|н|р] (%{ː%}:)  %> _ ; 

!!€ орон>{D}у{ː}
!!€ орон>дӯ

!!€ ю{ː}>{D}
!!€ ю̄>д

!!€ урэ>{D}ук
!!€ урэ>дук

"Nasal assimilation of V"
!! **@RULENAME@**  
%{V%}:м <=> :Nasal %> _ ; 

"Default realisaton of V" 
!! **@RULENAME@**  
%{V%}:в <=> [ :Vow | %{ː%}: | [:Cns - :Nasal]]  %> _ ; 

!!€ урэ>{i}л>{V}{A}
!!€ урэ>0л>вэ

"Stem assimilation to suffix"
н:р <=> _ %> [:в|:л|:д|:Vow];

!!€ орон>{V}{A}
!!€ орор>во

!! ### Palatalisation

"Not realising hard sign after vowel or n"
!! **@RULENAME@**  
%{j%}:0 <=> [ Vow | н | л | %{ː%}: ] %> _ ;

!!€ дю{ː}>{j}{A}>{i}в
!!€ дю̄>0я>0в

!!€ урэ>{i}л>{A}
!!€ урэ>0л>э

"Realising hard sign"
!! **@RULENAME@**  
%{j%}:ъ <=> NonLN %> _ [ Vow | %{A%}: ] ;

!!€ урэ>{j}{A}
!!€ урэ>0е

!!€ бэр>{j}{A}
!!€ бэр>ъе

!!€ бэр>{V}{A}
!!€ бэр>вэ

!!€ урэ>{V}{A}
!!€ урэ>вэ

!!€ дю{ː}>{V}{A}>{i}в
!!€ дю̄>ва00в

!!€ дю{ː}>{D}у{ː}>{i}в
!!€ дю̄>дӯ>0в

!htest с а %{ː%}:̄ >:> м и %{ː%}:̄

!! ### Other vowel rules

"Epenthetic vowel u-i-0 before present tense markers u"
%{U%}:у <=> :VoicedCns1 _ %> ;

"Epenthetic vowel u-i-0 before present tense markers i"
!%{U%}:и <=> [:UnvoicedCns|л|ӈ|н] _ %> ;
%{U%}:и <=> [:UnvoicedCns|л|м|ӈ|н] _ %> ; ! м in VoicedCns1 ?

"Epenthetic vowel u-i-0 before present tense markers 0"
%{U%}:0 <=> Vow (%{ː%}:) _ %> ;

!htest с а %{ː%}:̄ %{U%}:0 >:>


!! ## Consonant rules

!! ### Other cns

"Present tense realised as ra"
%{R%}:р <=> [:Vow (%{ː%}:)|:VoicedCns1] %> _ ;

!!€ са{ː}>{R}{A}>н
!!€ са̄>ра0н
!htest с а {ː}:̄ >:> {R}:р {A}:а >:> н

"Present tense realised as ta"
%{R%}:т <=> :UnvoicedCns %> _ ;

!!€ ис>{R}{A}>н
!!€ ис>та0н
!htest и с >:> {R}:т {A}:а >:0 н

"Present tense realised as la"
%{R%}:л <=> л %> _ ;

!!€ мел>{R}{A}>н
!!€ мел>ла>н
!htest м е {ː}:̄ л >:0 {R}:л {A}:а >:0 н

"Present tense realised as na"
%{R%}:н <=> [м|ӈ] %> _ ;

!!€ уӈ>{R}{A}>н
!!€ уӈ0нэ>н
!htest у ӈ >:0 {R}:н {A}:э >:> н

"Present tense realised as a"
%{R%}:0 <=> н %> _ ;

!!€ гун>{R}{A}>н
!!€ гун>0э>н
!htest г у н >:> {R}:0 {A}:э >:> н

!! ## Vowel rules

!! ### Vowel harmony for а

"Vowel harmony for A to а, main rule"
!! **@RULENAME@**  (note е->а)
%{A%}:а <=> :Vx (%{ː%}:) (:Cns)                (%{U%}:) %> ([(%{i%}:) :Cns %>:]) ([:Cns (:Cns)|(:Cns)])  _ ;
             :Vx (%{ː%}:) (:Cns) ([:и|:ы] :Cns) (%{U%}:) %> ([(%{i%}:) :Cns %>:]) ([:Cns (:Cns)|(:Cns)])  _ ;
             :Vx (%{ː%}:) (:Cns) (%> %{i%}: л)  (%{U%}:) %>   (%{i%}:) :Cns* :Vow* :Cns*                  _ ; !Pl
             :Vx (%{ː%}:)  :Cns                 (%{U%}:) %>                                   (ArchiCns:) _ ;
         where Vx in ( а е я ю ) ;

"Vowel harmony for {A} following i"
!! **@RULENAME@**  
%{A%}:а <=> .#. Cns:* [:и|:ы] (:Cns)                %> ([(%{i%}:) :Cns %>:]) (:Cns (:Cns) (%>:))  _ ;
            .#. Cns:* [:и|:ы] (:Cns) (%> %{i%}: л) %>   (%{i%}:) :Cns* :Vow* :Cns*  _ ;

									      
"Vowel harmony for A following long o"
!! **@RULENAME@**  long o gives a
%{A%}:а <=> о %{ː%}: (:Cns)                (%{U%}:) %> ([(%{i%}:) :Cns %>:]) ([:Cns (:Cns)|(:Cns)])  _ ;
            о %{ː%}: (:Cns) ([:и|:ы] :Cns) (%{U%}:) %> ([(%{i%}:) :Cns %>:]) ([:Cns (:Cns)|(:Cns)])  _ ;
            о %{ː%}: :Cns                  (%{U%}:) %> (ArchiCns:) _ ;
            о %{ː%}: (:Cns) (%> %{i%}: л)  (%{U%}:) %> (%{i%}:) :Cns* :Vow* :Cns* _ ; !Pl

"Vowel harmony for A to о, main rule"
!! **@RULENAME@**  (note е->а)
%{A%}:о <=> [о|:ё] (:Cns)                (%{U%}:) %> ([(%{i%}:) :Cns %>:]) ([:Cns (:Cns)|(:Cns)])  _ ;
            [о|:ё] (:Cns) ([:и|:ы] :Cns) (%{U%}:) %> ([(%{i%}:) :Cns %>:]) ([:Cns (:Cns)|(:Cns)])  _ ;
            [о|:ё] (:Cns) (%> %{i%}: л)  (%{U%}:) %>   (%{i%}:) :Cns* :Vow*  :Cns*            _ ; !Pl
            [о|:ё]  :Cns                 (%{U%}:) %>                                   (ArchiCns:) _ ;
            [о|:ё] ([:NonLN]) %> ([(%{i%}:) :Cns  %>:]) %{j%}:  _ ;
            [о|:ё] н:                             %> р :Cns*    _ ;
	    
"Palatalised short vowel harmony for A"
!! **@RULENAME@**  
!%{A%}:Vy <=> :Vx (%{ː%}:) ([:NonLN]) %> ([(%{i%}:) :NonLN %>:]) %{j%}:  _ ;
 %{A%}:Vy <=> :Vx (%{ː%}:) ([:NonLN]) %> ([(%{i%}:) :Cns   %>:]) %{j%}:  _ ;
         where Vx in ( а е э я ю )
               Vy in ( я е е я я )
         matched ;


!!€ орон>{j}{A}
!!€ орон>0о

"Vowel harmony for A to э, main rule"
!! **@RULENAME@**  (note е->а)
%{A%}:э <=> [:э|у] (%{ː%}:) (:Cns)                (%{U%}:) %> ([(%{i%}:) :Cns %>:]) ([:Cns (:Cns)|(:Cns)])  _ ;
            [:э|у] (%{ː%}:) (:Cns) ([:и|:ы] :Cns) (%{U%}:) %> ([(%{i%}:) :Cns %>:]) ([:Cns (:Cns)|(:Cns)])  _ ;
            [:э|у] (%{ː%}:) (:Cns) (%> %{i%}: л)  (%{U%}:) %>   (%{i%}:) :Cns* :Vow* :Cns*                  _ ; !Pl
            [:э|у] (%{ː%}:)  :Cns                 (%{U%}:) %>                                   (ArchiCns:) _ ;

!!€ урэ>{i}кл{A}{ː}
!!€ урэ>0клэ̄

!!€ урэ>{i}л>{D}ул{A}{ː}
!!€ урэ>0л>дулэ̄

!!€ бэр>{D}ул{A}{ː}
!!€ бэр>дулэ̄

!!€ тэтыврэв
!!€ тэтыврэв
!htest т э т ы в >:> {R}:р {A}:э в




!htest с а {ː}:̄ >:0 м и {ː}:̄

!!€ урэ>{i}л>{A}
!!€ урэ>0л0э

!!€ урэ>{V}{A}
!!€ урэ>вэ

!!€ орон>{V}{A}
!!€ орон>мо

!!€ орон>р{V}{A}
!!€ оро0>рво

!!€ test
!!€ fail






! Old rules for ref


!а̄ е̄ ӣ э̄ ӯ о̄
!А̄ Е̄ Ӣ ¯Э Ӯ О̄

!"Long vowel harmony for {A}"
!%{A%}:Vy <=> :Vx %{ː%}: [ :Cns* :Cns ]/:0*  _ ;
!         where Vx in ( а е ё о э ю я )
!               Vy in ( э а о а а э э )
!         matched ;

!"Vowel harmony for A, main rule"
!!! __@RULENAME@__  (note е->а)
!%{A%}:Vy <=> :Vx (%{ː%}:) (:Cns)                (%{U%}:) %> ([(%{i%}:) :Cns %>:]) ([:Cns (:Cns)|(:Cns)])  _ ;
!             :Vx (%{ː%}:) (:Cns) ([:и|:ы] :Cns) (%{U%}:) %> ([(%{i%}:) :Cns %>:]) ([:Cns (:Cns)|(:Cns)])  _ ;
!             :Vx (%{ː%}:) (:Cns) (%> %{i%}: л)  (%{U%}:) %>   (%{i%}:) :Cns* :Vow* :Cns*                  _ ; !Pl
!             :Vx (%{ː%}:)  :Cns                 (%{U%}:) %>                                   (ArchiCns:) _ ;
!         where Vx in ( а е я ю ё э )
!               Vy in ( а а а а о э )
!         matched ;

!"Vowel harmony for A following o"
!!! __@RULENAME@__  short o gives o
!%{A%}:о <=> о (:Cns)                (%{U%}:) %> ([(%{i%}:) :Cns %>:]) ([:Cns (:Cns)|(:Cns)])  _ ;
!            о (:Cns) ([:и|:ы] :Cns) (%{U%}:) %> ([(%{i%}:) :Cns %>:]) ([:Cns (:Cns)|(:Cns)])  _ ;
!            о :Cns                  (%{U%}:) %> (ArchiCns:) _ ;
!            о (:Cns) (%> %{i%}: л) (%{U%}:) %> (%{i%}:) :Cns* :Vow* :Cns* _ ; !Pl
