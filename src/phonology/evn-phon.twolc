! ================================================== !
!! !!!The Evenki morphophonological/twolc rules file 
! ================================================== !

!! !!Alphabet
Alphabet

 а б в г д е ё ж з и й к л м н ӈ о п р с т у ф х ц ч ш щ ъ ы ь э ю я  !!= * @CODE@
 А Б В Г Д Е Ё Ж З И Й К Л М Н Ӈ О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я  !!= * @CODE@

!а̄ е̄ ӣ э̄ ӯ о̄
!А̄ Е̄ Ӣ ¯Э Ӯ О̄


 %{ː%}:̄  !!= * @CODE@

 %{L%}:л %{L%}:р								     !!= * @CODE@
 %{G%}:г %{G%}:к %{G%}:ӈ						     !!= * @CODE@
 %{D%}:д %{D%}:т 								     !!= * @CODE@
 %{A%}:а %{A%}:э %{A%}:о  						     !!= * @CODE@
 %{Я%}:я %{Я%}:е %{Я%}:ё %{Я%}:а %{Я%}:э %{Я%}:о     !!= * @CODE@
 %{V%}:в %{V%}:м								     !!= * @CODE@
 %{R%}:р %{R%}:0 %{R%}:т %{R%}:л %{R%}:н 		     !!= * @CODE@
 
 %{j%}:0  %{j%}:ъ              !!= * @CODE@
 %{i%}:0 %{i%}:и %{i%}:ы          !!= * @CODE@
 %{U%}:и %{U%}:у  %{U%}:0  !!= * @CODE@
 %>:0  ;       !!= * @CODE@

!! !! Sets
Sets

Vow = а е ё и о у ы э ю я 						       !!= * @CODE@
      А Е Ё И О У Ы Э Ю Я 						       !!= * @CODE@
      а̄ е̄ ӣ э̄ ӯ о̄							       !!= * @CODE@
      А̄ Е̄ Ӣ ¯Э Ӯ О̄ ;						       !!= * @CODE@

Cns = б в г д ж з й к л м н ӈ п р с т ф х ц ч ш щ      !!= * @CODE@
      Б В Г Д Ж З Й К Л М Н Ӈ П Р С Т Ф Х Ц Ч Ш Щ ;    !!= * @CODE@
ArchiCns = %{D%} %{G%} %{L%} %{R%} %{V%} ;			       !!= * @CODE@
Nasal = м н ӈ									       !!= * @CODE@
        М Н Ӈ ; 								       !!= * @CODE@

NonLN = б в г д ж з й к м ӈ п р с т ф х ц ч ш щ        !!= * @CODE@
        Б В Г Д Ж З Й К М Ӈ П Р С Т Ф Х Ц Ч Ш Щ ;      !!= * @CODE@

UnvoicedCns = к с т п ;    !!= * @CODE@
VoicedCns1 = б в г д щ з й м р ; 


!! !!Rules
Rules 

!! !Assimilation rules

"Remove suffix vowel with vowel-ending stem"
!! __@RULENAME@__  
%{i%}:0 <=>  :Vow (%{ː%}:) %>: _ ;

"Realise suffix vowel as y after t"
!! __@RULENAME@__  
%{i%}:ы <=> :т %>: _ ;

"Realise suffix vowel as i after non-t"
!! __@RULENAME@__  
%{i%}:и <=> [:Cns - :т] %>: _ ;

"Assimilate possessive to plural"
!! __@RULENAME@__  
%{L%}:Cx <=> Cx %>: _ ;
       where Cx in ( л р ) ;

"Consonant assimilation in suffixes"
!! __@RULENAME@__  
%{G%}:Cx <=> Cy %>: _ ;
       where Cy in ( к с т п м н ӈ ) 
             Cx in ( к к к к ӈ ӈ ӈ ) 
       matched ; 

"Unvoicing assimilation"
!! __@RULENAME@__  
%{D%}:т <=> :UnvoicedCns %>: _ ; 

!!€ дэт>{D}ук
!!€ дэт0тук


"Voicing assimilation"
!! __@RULENAME@__  
!%{D%}:д <=> [:Vow| %{ː%}: | [:Cns - :UnvoicedCns]] %>: _ ; 
%{D%}:д <=> [Vow|л|р] (%{ː%}:)  %>: _ ; 

!!€ ю{ː}>{D}
!!€ ю̄0д


!!€ урэ>{D}ук
!!€ урэ0дук

"Nasal assimilation of V"
!! __@RULENAME@__  
%{V%}:м <=> :Nasal %>: _ ; 

"Default realisaton of V" 
!! __@RULENAME@__  
%{V%}:в <=> [ :Vow | %{ː%}: | [:Cns - :Nasal]]  %>: _ ; 

!!€ урэ>{i}л>{V}{A}
!!€ урэ00л0вэ


!! !Palatalisation


"Not realising hard sign after vowel or n"
!! __@RULENAME@__  
%{j%}:0 <=> [ Vow | н | л | %{ː%}: ] %>: _ ;

!!€ дю{ː}>{j}{A}>{i}в
!!€ дю̄00я00в

"Realising hard sign"
!! __@RULENAME@__  
%{j%}:ъ <=> NonLN %>: _ [ Vow | %{A%}: ] ;

!!€ урэ>{j}{A}
!!€ урэ00е

!!€ бэр>{j}{A}
!!€ бэр0ъе

!!€ бэр>{V}{A}
!!€ бэр0вэ

!!€ урэ>{V}{A}
!!€ урэ0вэ

!!€ дю{ː}>{V}{A}>{i}в
!!€ дю̄0ва00в

!!€ дю{ː}>{D}у{ː}>{i}в
!!€ дю̄0дӯ00в

!htest с а %{ː%}:̄ >:0 м и %{ː%}:̄

!! !Other vowel rules

"Epenthetic vowel u-i-0 before present tense markers u"
%{U%}:у <=> :VoicedCns1 _ %>: ;

"Epenthetic vowel u-i-0 before present tense markers i"
%{U%}:и <=> [:UnvoicedCns|л|м|ӈ|н] _ %>: ;

"Epenthetic vowel u-i-0 before present tense markers 0"
%{U%}:0 <=> Vow (%{ː%}:) _ %>: ;

!htest с а %{ː%}:̄ %{U%}:0 >:0 


!! !!Consonant rules

!! !Other cns

"Present tense realised as ra"
%{R%}:р <=> [:Vow (%{ː%}:)|:VoicedCns1] %>: _ ;

!!€ са{ː}>{R}{A}>н
!!€ са̄0ра0н
!htest с а {ː}:̄ >:0 {R}:р {A}:а >:0 н

"Present tense realised as ta"
%{R%}:т <=> :UnvoicedCns %>: _ ;

!!€ ис>{R}{A}>н
!!€ ис0та0н
!htest и с >:0 {R}:т {A}:а >:0 н

"Present tense realised as la"
%{R%}:л <=> л %>: _ ;

!!€ мел>{R}{A}>н
!!€ мел0ла0н
!htest м е {ː}:̄ л >:0 {R}:л {A}:а >:0 н

"Present tense realised as na"
%{R%}:н <=> [м|ӈ] %>: _ ;

!!€ уӈ>{R}{A}>н
!!€ уӈ0нэ0н
!htest у ӈ >:0 {R}:н {A}:э >:0 н

"Present tense realised as a"
%{R%}:0 <=> н %>: _ ;

!!€ гун>{R}{A}>н
!!€ гун00э0н
!htest г у н >:0 {R}:0 {A}:э >:0 н

!! !Vowel harmony
! TODO: Short/Long

!"Long vowel harmony for {A}"
!%{A%}:Vy <=> :Vx %{ː%}: [ :Cns* :Cns ]/:0*  _ ;
!         where Vx in ( а е ё о э ю я )
!               Vy in ( э а о а а э э )
!         matched ;

"Vowel harmony for A, main rule"
!! __@RULENAME@__  (note е->а)
%{A%}:Vy <=> :Vx (%{ː%}:) (:Cns)                (%{U%}:) %>: ([(%{i%}:) :Cns %>:]) ([:Cns (:Cns)|(:Cns)])  _ ;
             :Vx (%{ː%}:) (:Cns) ([:и|:ы] :Cns) (%{U%}:) %>: ([(%{i%}:) :Cns %>:]) ([:Cns (:Cns)|(:Cns)])  _ ;
             :Vx (%{ː%}:)  :Cns                 (%{U%}:) %>: (ArchiCns:) _ ;
             :Vx (%{ː%}:) (:Cns) (%>: %{i%}: л) (%{U%}:) %>: (%{i%}:) :Cns* :Vow* :Cns* _ ; !Pl
         where Vx in ( а е ё у э я ю )
               Vy in ( а а о э э а а )
         matched ;

!!€ урэ>{i}кл{A}{ː}
!!€ урэ00клэ̄

!!€ урэ>{i}л>{D}ул{A}{ː}
!!€ урэ00л0дулэ̄

!!€ бэр>{D}ул{A}{ː}
!!€ бэр0дулэ̄

!!€ тэтыврэв
!!€ тэтыврэв
!htest т э т ы в >:> {R}:р {A}:э в

"Vowel harmony for {A} following i"
!! __@RULENAME@__  
%{A%}:а <=> .#. Cns:* [:и|:ы] (:Cns)                %>: ([(%{i%}:) :Cns %>:]) (:Cns (:Cns) (%>:))  _ ;
            .#. Cns:* [:и|:ы] (:Cns) (%>: %{i%}: л) %>:   (%{i%}:) :Cns* :Vow* :Cns*  _ ;

"Vowel harmony for A following o"
!! __@RULENAME@__  short o gives o
%{A%}:o <=> о (:Cns)                (%{U%}:) %>: ([(%{i%}:) :Cns %>:]) ([:Cns (:Cns)|(:Cns)])  _ ;
            о (:Cns) ([:и|:ы] :Cns) (%{U%}:) %>: ([(%{i%}:) :Cns %>:]) ([:Cns (:Cns)|(:Cns)])  _ ;
            о :Cns                  (%{U%}:) %>: (ArchiCns:) _ ;
            о (:Cns) (%>: %{i%}: л) (%{U%}:) %>: (%{i%}:) :Cns* :Vow* :Cns* _ ; !Pl

"Vowel harmony for A following long o"
!! __@RULENAME@__  long o gives a
%{A%}:а <=> о %{ː%}: (:Cns)                (%{U%}:) %>: ([(%{i%}:) :Cns %>:]) ([:Cns (:Cns)|(:Cns)])  _ ;
            о %{ː%}: (:Cns) ([:и|:ы] :Cns) (%{U%}:) %>: ([(%{i%}:) :Cns %>:]) ([:Cns (:Cns)|(:Cns)])  _ ;
            о %{ː%}: :Cns                  (%{U%}:) %>: (ArchiCns:) _ ;
            о %{ː%}: (:Cns) (%>: %{i%}: л) (%{U%}:) %>: (%{i%}:) :Cns* :Vow* :Cns* _ ; !Pl


"Palatalised short vowel harmony for A"
!! __@RULENAME@__  
!%{A%}:Vy <=> :Vx (%{ː%}:) ([:NonLN]) %>: ([(%{i%}:) :NonLN %>:]) %{j%}:  _ ;
 %{A%}:Vy <=> :Vx (%{ː%}:) ([:NonLN]) %>: ([(%{i%}:) :Cns   %>:]) %{j%}:  _ ;
         where Vx in ( а е ё о э я ю )
               Vy in ( я е ё ё е я я )
         matched ;

!!€ урэ>{i}л>{j}{A}
!!€ урэ00л00е

!!€ орон>{j}{A}
!!€ орон00о

!htest с а {ː}:̄ >:0 м и {ː}:̄


!!€ test
!!€ fail

